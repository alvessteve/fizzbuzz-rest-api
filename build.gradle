plugins {
    id 'org.springframework.boot' version '3.0.6'
    id 'org.graalvm.buildtools.native' version '0.9.20'
    id 'io.freefair.lombok' version '6.5.1'
    id 'org.flywaydb.flyway' version '9.8.1'
    id 'java'
}

apply plugin: 'io.spring.dependency-management'

group = 'com.training'
version = '0.0.3'
sourceCompatibility = '17'

sourceSets {
    integration {
        java.srcDir "$projectDir/src/integration/java"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime

    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven { url = uri("https://repo.spring.io/release") }
    gradlePluginPortal()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
    implementation 'org.flywaydb:flyway-core'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'
    runtimeOnly 'org.postgresql:postgresql'

    annotationProcessor 'org.projectlombok:lombok'

    integrationImplementation 'io.rest-assured:rest-assured:5.3.0'
    integrationImplementation 'io.rest-assured:xml-path:5.3.0' //use to prevent dependency conflicts on groovy-xml dep
    integrationImplementation "org.testcontainers:junit-jupiter:1.18.0"
    integrationImplementation "org.testcontainers:testcontainers:1.18.0"
    integrationImplementation "org.testcontainers:postgresql:1.18.0"
    integrationImplementation 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:4.8.0'
    testImplementation 'org.assertj:assertj-core:3.23.1'
}

test {
    useJUnitPlatform()
}

tasks.withType(Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

tasks.withType(Copy).all {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
}

check.dependsOn integrationTest


